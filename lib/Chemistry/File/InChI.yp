# Header section

%%

# Rules section

# The grammar is taken from https://github.com/metamolecular/inchi-grammar/blob/master/grammar.ebnf, commit 74a8858, MIT license

string              ::= prefix formula? connections? h_atoms?
formula             ::= "/" elemental ( "." elemental )*
connections         ::= "/c" graph? ( ";" graph? )*
h_atoms             ::= "/h" hydrogens? ( ";" hydrogens? )*
elemental           ::= count? ( c h? b? br? cl? f? rest_elements )
                      | ( b? br? cl? f? h? rest_elements )
rest_elements       ::= i? mg? n? o? p? s?
b                   ::= "B" count?
c                   ::= "C" count?
h                   ::= "H" count?
br                  ::= "Br" count?
cl                  ::= "Cl" count?
f                   ::= "F" count?
i                   ::= "I" count?
mg                  ::= "Mg" count?
n                   ::= "N" count?
o                   ::= "O" count?
p                   ::= "P" count?
s                   ::= "S" count?
/* and so on... */
graph               ::= (count "*")? index body
body                ::= ( "-" | branch+ ) tail
branch              ::= "(" ( index ( ( "," index ) | body )* ) ")"
tail                ::= index body*
hydrogens           ::= (count "*")? ( vh_count ( "," mh_count )* ) | mh_count
vh_count            ::= virtual_hydrogens ( "," virtual_hydrogens )*
mh_count            ::= mobile_hydrogens+
virtual_hydrogens   ::= index ( ( "," |  "-" ) index )* "H" count?
mobile_hydrogens    ::= "(H" (count? "-" | count )? ( "," index )+ ")"
index               ::= nonzero digit*
count               ::= ( "1" digit+ ) | ( twoplus digit* )
digit               ::= "0" | nonzero
nonzero             ::= "1" | twoplus
prefix              ::= "InChI=1" "S"?
twoplus             ::= "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9"

%%

# Footer section

1;
